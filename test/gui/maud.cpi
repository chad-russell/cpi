#import "gui/gui";
#import "gui/text";
#import "io";
#import "buffer": buf;
#import "sdl";
#import "cairo";
#import "math";
#import "iter";
#import "mem";

PORT_RADIUS: f64 = 3;

type EID i64

type Screen struct {
    scale: f64 = 1.0,
    translate: gui.Point!(f64),

    nodes: buf.Buffer!(Node),
    ports: buf.Buffer!(Port),
    connections: buf.Buffer!(Connection),
    controls: buf.Buffer!(Control),
}
type ScreenRef *Screen

#context state: struct {
    transformedMouse: gui.Point!(f64),

    mouseDownStart: gui.Point!(f64),
    mouseDown := false,

    canvasBeingDragged := false,
    dragging := false,

    nodeBeingDragged: EID = -1,
    controlBeingDragged: EID = -1,
    portBeingDragged: EID = -1,
    tentativeConnection: EID = -1,

    screens: buf.Buffer!(ScreenRef),
    topScreen: ScreenRef,

    -- hackProofOfConcept: f32 = 440.0,
}

type Node struct {
    valid: bool,

    rect: gui.Rect,
    active: bool,

    inputs: buf.Buffer!(EID),
    outputs: buf.Buffer!(EID),

    screen: Screen,

    hasControl := false,
    controlId: EID,
}

type Port struct {
    valid: bool,

    isInput: bool,
    center: gui.Point!(f64),

    connections: i32 = 0,
    allowedConnections: i32 = 1,
}

type Control struct {
    valid: bool,

    rect: gui.Rect,

    value: i64,
    min: i64,
    max: i64,
}

type Connection struct {
    from: EID,
    to: EID,
}

fn addNode(n: Node) EID {
    for node, index: context.state.topScreen.nodes.buf {
        if node.valid == false {
            context.state.topScreen.nodes.buf[index] = n;
            return index;
        }
    }

    &context.state.topScreen.nodes|:append(n);
    return context.state.topScreen.nodes.buf.count - 1;
}

fn addPort(p: Port) EID {
    for port, index: context.state.topScreen.ports.buf {
        if port.valid == false {
            context.state.topScreen.ports.buf[index] = p;
            return index;
        }
    }

    &context.state.topScreen.ports|:append(p);
    return context.state.topScreen.ports.buf.count - 1;
}

fn init() {
    basic.initContext();

    gui.init();
    text.init();

    s: Screen;
    &context.state.screens|:append(mem.heap(s));
    context.state.topScreen = buf.top(&context.state.screens);

    addControlNode();

    while !context.quit {
        gui.tick();

        processMessages();
        draw(gui.windowRect());

        gui.commit();
    }

    return 0;
}

fn addControlNode() {
    -- make a new node
    n: Node;
    n.valid = true;
    n.rect = {context.state.transformedMouse.x, context.state.transformedMouse.y, 200, 100};
    n.active = true;
    n.hasControl = true;
    n.controlId = addControl();

    p: Port;
    p.valid = true;
    p.isInput = false;
    portId := addPort(p);
    &n.outputs|:append(portId);

    addNode(n);
}

fn addControl() EID {
    c: Control;
    c.valid = true;
    c.value = 678;
    c.min = 200;
    c.max = 800;

    for control, index: context.state.topScreen.controls.buf {
        if control.valid == false {
            context.state.topScreen.controls.buf[index] = c;
            return index;
        }
    }

    &context.state.topScreen.controls|:append(c);
    return context.state.topScreen.controls.buf.count - 1;
}

fn processMessages() {
    if context.multiGestureEvent != nil {
        pinch := cast(f64) context.multiGestureEvent.dDist * 5;

        newScale := context.state.topScreen.scale + pinch;
        if newScale < 0.5 {
            pinch = 0.5 - context.state.topScreen.scale;
        }
        if newScale > 1.5 {
            pinch = 1.5 - context.state.topScreen.scale;
        }

        context.state.topScreen.translate.x -= pinch * context.state.transformedMouse.x;
        context.state.topScreen.translate.y -= pinch * context.state.transformedMouse.y;

        context.state.topScreen.scale += pinch;
        if context.state.topScreen.scale < 0.5 {
            context.state.topScreen.scale = 0.5;
        }
        if context.state.topScreen.scale > 1.5 {
            context.state.topScreen.scale = 1.5;
        }
    }

    tmp_scale();
    defer { cairo.cairo_restore(context.cairo.layer.context); }

    if context.keyDownEvent != nil {
        keydown(context.keyDownEvent);
    }
    if context.mouseMotionEvent != nil {
        mouseMove(context.mouseMotionEvent);
    }
    if context.mouseButtonDownEvent != nil {
        mouseButtonDown(context.mouseButtonDownEvent);
    }
    if context.mouseButtonUpEvent != nil {
        mouseButtonUp(context.mouseButtonUpEvent);
    }
}

fn checkMouseDownInPort(port: *Port, id: EID) {
    if gui.distance(port.center, context.state.transformedMouse) < magFactor(port) * PORT_RADIUS {
        alreadyInConnection := false;
        c: Connection;
        cIndex: i64;

        for connection, i: context.state.topScreen.connections.buf {
            if connection.from == id or connection.to == id {
                alreadyInConnection = true;
                c = connection;
                cIndex = i;
            }
        }

        if alreadyInConnection == true {
            removeConnection(cIndex);

            if c.from == id {
                context.state.portBeingDragged = c.to;
            }
            else {
                context.state.portBeingDragged = c.from;
            }
        }
        else {
            context.state.nodeBeingDragged = -1;
            context.state.portBeingDragged = id;
        }
    }
}

fn mouseButtonDown(messageData: *sdl.MouseButtonEvent) {
    context.state.mouseDownStart = context.state.transformedMouse;
    context.state.mouseDown = true;
    context.state.dragging = false;

    context.state.nodeBeingDragged = -1;
    context.state.portBeingDragged = -1;

    mouseDownInPort := false;
    for port, index: context.state.topScreen.ports.buf {
        if port.valid == true {
            checkMouseDownInPort(&port, index);
        }
    }

    mouseDownInControl := false;
    for control, index: context.state.topScreen.controls.buf {
        if control.valid == true and control.rect|gui.contains(context.state.transformedMouse.x, context.state.transformedMouse.y) == true {
            mouseDownInControl = true;
            context.state.controlBeingDragged = index;
        }
    }

    if mouseDownInPort == false and mouseDownInControl == false {
        index := 0;
        for node: context.state.topScreen.nodes.buf|iter.byRef {
            if node.valid == true and node.active == true {
                if context.modifierKeys.ctrl == true {
                    &context.state.screens|:append(&node.screen);
                    context.state.topScreen = &context.state.screens|buf.top;
                }
                else {
                    context.state.nodeBeingDragged = index;
                }
            }
            index += 1;
        }
    }
}

fn mouseMove(messageData: *sdl.MouseMotionEvent) {
    context.state.transformedMouse = context.mouse;
    cairo.cairo_device_to_user(context.cairo.layer.context, &context.state.transformedMouse.x, &context.state.transformedMouse.y);

    dx := context.state.transformedMouse.x - context.state.mouseDownStart.x;
    dy := context.state.transformedMouse.y - context.state.mouseDownStart.y;

    if context.state.mouseDown {
        context.state.dragging = true;
        context.state.mouseDownStart = context.state.transformedMouse;
    }

    context.state.tentativeConnection = -1;
    if context.state.portBeingDragged != -1 {
        -- establish a connection?
        for port, portIndex: context.state.topScreen.ports.buf {
            if port.valid == true {
                mf := magFactor(&port);

                if portIndex != context.state.portBeingDragged and gui.distance(context.state.transformedMouse, port.center) < PORT_RADIUS * mf {
                    pbd := findPort(context.state.portBeingDragged);

                    if (pbd.isInput == true and port.isInput == false) or (pbd.isInput == false and port.isInput == true) {
                        context.state.tentativeConnection = portIndex;
                    }
                }
            }
        }
    }
    else if context.state.nodeBeingDragged != -1 {
        nbd := findNode(context.state.nodeBeingDragged);

        nbd.active = true;
        nbd.rect.x += dx;
        nbd.rect.y += dy;
    }
    else if context.state.controlBeingDragged != -1 {
        c := findControl(context.state.controlBeingDragged);

        c.value += cast() dx;
        if c.value > c.max { c.value = c.max; }
        if c.value < c.min { c.value = c.min; }

        -- context.state.hackProofOfConcept = cast() c.value;
    }
    else if context.state.mouseDown {
        context.state.canvasBeingDragged = true;

        for node: context.state.topScreen.nodes.buf|iter.byRef {
            if node.valid == true {
                node.rect.x += dx;
                node.rect.y += dy;
            }
        }
    }
    else {
        for node: context.state.topScreen.nodes.buf|iter.byRef {
            if node.valid == true {
                if node.rect|gui.contains(context.state.transformedMouse.x, context.state.transformedMouse.y) {
                    node.active = true;
                }
                else {
                    node.active = false;
                }
            }
        }
    }
}

fn mouseButtonUp(messageData: *sdl.MouseButtonEvent) {
    context.state.mouseDown = false;
    context.state.canvasBeingDragged = false;

    if context.state.tentativeConnection != -1 {
        from := findPort(context.state.portBeingDragged);
        to := findPort(context.state.tentativeConnection);

        if from.connections < from.allowedConnections and to.connections < to.allowedConnections {
            from.connections += 1;
            to.connections += 1;

            &context.state.topScreen.connections|:append({
                from: context.state.portBeingDragged,
                to: context.state.tentativeConnection,
            });
        }

        context.state.tentativeConnection = -1;
    }
    else if messageData.button == sdl.MouseButton.RIGHT {
        -- delete node
        -- todo(chad): recursively delete the screen associated with that node
        -- todo(chad): delete any controls associated with that node

        valid := true;
        if context.state.nodeBeingDragged == -1 {
            valid = false;
        }

        node: *Node;
        if valid == true {
            node = findNode(context.state.nodeBeingDragged);
            valid = node.valid;
        }

        if valid {
            -- delete any connections that involve a node's inputs or outputs
            index := context.state.topScreen.connections.buf.count - 1;
            while index >= 0 {
                c := context.state.topScreen.connections.buf[index];
                shouldDelete := false;

                for p: node.inputs.buf {
                    if p == c.to or p == c.from {
                        shouldDelete = true;
                    }
                }
                for p: node.outputs.buf {
                    if p == c.to or p == c.from {
                        shouldDelete = true;
                    }
                }

                if shouldDelete == true {
                    removeConnection(index);
                }

                index -= 1;
            }

            -- delete ports from the node
            for p: node.inputs.buf {
                context.state.topScreen.ports.buf[p].valid = false;
            }
            for p: node.outputs.buf {
                context.state.topScreen.ports.buf[p].valid = false;
            }

            -- delete the node
            context.state.topScreen.nodes.buf[context.state.nodeBeingDragged].valid = false;
        }
    }
    else if context.state.dragging == false and context.modifierKeys.ctrl == false {
        -- make a new node
        n: Node;
        n.valid = true;
        n.rect = {context.state.transformedMouse.x, context.state.transformedMouse.y, 200, 100};
        n.active = true;

        fn addPortToNode(n: *Node, b: *buf.Buffer!(EID), isInput: bool) {
            p: Port;
            p.valid = true;
            p.isInput = isInput;
            portId := addPort(p);
            b|:append(portId);
        }

        addPortToNode(&n, &n.inputs, true);
        addPortToNode(&n, &n.inputs, true);

        addPortToNode(&n, &n.outputs, false);
        addPortToNode(&n, &n.outputs, false);
        addPortToNode(&n, &n.outputs, false);
        addPortToNode(&n, &n.outputs, false);

        addNode(n);
    }

    context.state.nodeBeingDragged = -1;
    context.state.portBeingDragged = -1;
    context.state.controlBeingDragged = -1;

    context.state.dragging = false;
}

fn removeConnection(index: i64) {
    connection := context.state.topScreen.connections.buf[index];

    from := findPort(connection.from);
    to := findPort(connection.to);

    from.connections -= 1;
    to.connections -= 1;

    &context.state.topScreen.connections|buf.deleteByReordering(index);
}

fn keydown(messageData: *sdl.KeyboardEvent) {
    if messageData.keysym.scancode == sdl.Key.UP {
        if context.state.screens.buf.count > 1 {
            &context.state.screens|buf.pop;
            context.state.topScreen = &context.state.screens|buf.top;
        }
    }
}

fn tmp_scale() {
    cairo.cairo_save(context.cairo.layer.context);
    cairo.cairo_translate(context.cairo.layer.context, context.state.topScreen.translate.x, context.state.topScreen.translate.y);
    cairo.cairo_scale(context.cairo.layer.context, context.state.topScreen.scale, context.state.topScreen.scale);
}

fn findNode(id: EID) *Node {
    basic.assertMsg(id >= 0 and id < context.state.topScreen.nodes.buf.count, "invalid node id");
    return &context.state.topScreen.nodes.buf[id];
}

fn findPort(id: EID) *Port {
    basic.assertMsg(id >= 0 and id < context.state.topScreen.ports.buf.count, "invalid port id");
    return &context.state.topScreen.ports.buf[id];
}

fn findControl(id: EID) *Control {
    basic.assertMsg(id >= 0 and id < context.state.topScreen.controls.buf.count, "invalid connection id");
    return &context.state.topScreen.controls.buf[id];
}

fn magFactor(port: *Port) {
    mf: f64 = 1.0;

    dist := gui.distance(port.center, context.state.transformedMouse);
    if dist < 20 {
        dist /= 10;
        mf = math.min(5 - (dist * dist), 2.5);
    }

    return mf;
}

fn drawNode(node: *Node) {
    fontSize: f64 = 78;

    nodeColor: gui.Color = {cast(f64) 167.0/255.0, cast(f64) 98.0/255.0, cast(f64) 82.0/255.0};
    if node.active == true or context.state.canvasBeingDragged == true {
        nodeColor = {cast(f64) 98.0/255.0, cast(f64) 167.0/255.0, cast(f64) 82.0/255.0};
    }

    gui.drawRoundedRect(context.cairo.layer,
        node.rect,
        nodeColor.r, nodeColor.g, nodeColor.b,
        radius: PORT_RADIUS, lineWidth: 5);

    -- draw ports
    drawPorts(node, nodeColor, node.inputs.buf, node.rect.x + 15);
    drawPorts(node, nodeColor, node.outputs.buf, node.rect.x + node.rect.width - 15);

    if node.hasControl == true {
        control := findControl(node.controlId);

        -- bounding rect for controls
        maxWidth := node.rect.width/2 - 20;
        maxHeight := node.rect.height;
        bound := math.min(maxWidth, maxHeight);

        control.rect = {node.rect.x + node.rect.width/2 - bound/2, node.rect.y + node.rect.height/2 - bound/2, bound, bound};

        -- draw circle
        gap := 2 * math.PI * 0.2;
        start := 2 * math.PI * 0.25 + gap/2;
        hundredPercent := 2 * math.PI - gap;

        controlPercent := cast(f64) (control.value - control.min) / cast(f64) (control.max - control.min);

        gui.drawArc(context.cairo.layer,
            {control.rect.x + control.rect.width/2, control.rect.y + control.rect.height/2},
            radius: bound/2,
            ø1: start,
            ø2: start + hundredPercent * controlPercent,
            255.0, 255.0, 255.0,
            lineWidth: 3);

        text.drawBoundedText(control.value|io.toString, control.rect|gui.inset(10));
    }
}

fn drawPorts(node: *Node, color: gui.Color, ports: []EID, x: f64) {
    portSpacing := node.rect.height / cast() ports.count;
    totalPortSpace := portSpacing * cast() (ports.count - 1);
    totalSpace := node.rect.height;
    yOffset: f64 = (totalSpace - totalPortSpace) / 2;

    for portId: ports {
        port := findPort(portId);

        port.center = {x, node.rect.y + yOffset};
        yOffset += portSpacing;

        mf := magFactor(port);

        gui.drawCircle(context.cairo.layer,
            port.center,
            PORT_RADIUS * mf,
            color.r, color.g, color.b,
            lineWidth: 1);
    }
}

fn drawConnection(connection: Connection) {
    p1 := findPort(connection.from).center;
    p2 := findPort(connection.to).center;

    gui.drawCurve(context.cairo.layer,
        p1: p1,
        p2: p2,
        cp1: cast() {
            x: gui.lerp(p1.x, p2.x, 0.3),
            y: p1.y,
        },
        cp2: cast() {
            x: gui.lerp(p1.x, p2.x, 0.7),
            y: p2.y,
        },
        color: {
            r: cast(f64) 250.0/255.0,
            g: cast(f64) 215.0/255.0,
            b: cast(f64) 0.0/255.0,
        },
        width: 4,
    );
}

fn draw(rect: gui.Rect) {
    -- background
    gui.drawRect(context.cairo.layer, rect, cast(f64) 67.0/255.0, cast(f64) 77.0/255.0, cast(f64) 82.0/255.0);

    tmp_scale();
    defer { cairo.cairo_restore(context.cairo.layer.context); }

    -- connections
    for connection: context.state.topScreen.connections.buf {
        drawConnection(connection);
    }
    if context.state.portBeingDragged != -1 {
        port := findPort(context.state.portBeingDragged);

        p1 := port.center;
        p2 := context.state.transformedMouse;

        cp1: gui.Point!(f64) = {gui.lerp(p1.x, p2.x, 0.3), p1.y};
        cp2: gui.Point!(f64) = {gui.lerp(p1.x, p2.x, 0.7), p2.y};

        color: gui.Color = {
            r: cast(f64) 250.0/255.0,
            g: cast(f64) 215.0/255.0,
            b: cast(f64) 0.0/255.0,
        };

        gui.drawCurve(context.cairo.layer,
            p1: p1,
            p2: p2,
            cp1: cp1,
            cp2: cp2,
            color: color,
            width: 4,
        );
    }

    -- nodes
    for node: context.state.topScreen.nodes.buf|iter.byRef {
        if node.valid == true {
            drawNode(node);
        }
    }
}
