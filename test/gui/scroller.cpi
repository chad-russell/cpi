#import "gui/gui";
#import "sdl";
#import "io";
#import "math";

type Data struct {
    rect: gui.Rect,

    horizontal: bool,

    begin: f64,
    end: f64 = 100,

    mouseOverBeginResize: bool,
    mouseOverEndResize: bool,

    resizingBegin: bool,
    resizingEnd: bool,

    dragging: bool,
    dragStartBegin: f64,
    dragStartEnd: f64,
    dragStartMouse: f64
}

fn scrollMax(data: *Data) f64 {
    if data.horizontal == true {
        return data.rect.width - (data.end - data.begin);
    }

    return data.rect.height - (data.end - data.begin);
}

fn percScrolled(data: *Data) f64 {
    sm := scrollMax(data);

    if sm == 0 {
        return 0;
    }

    return data.begin / sm;
}

fn effectiveRect(data: *Data) gui.Rect {
    if data.horizontal == true {
        return {
            data.rect.x + data.begin,
            data.rect.y,
            data.end - data.begin,
            data.rect.height
        };
    }

    return {
        data.rect.x,
        data.rect.y + data.begin,
        data.rect.width,
        data.end - data.begin
    };
}

fn drawVertical(rect: gui.Rect, data: *Data) {
    data.horizontal = false;
    data.rect = rect;

    er := effectiveRect(data);

    gui.drawRect(context.cairo.layer,
        er,
        cast(f64) 107.0/255.0, cast(f64) 117.0/255.0, cast(f64) 123.0/255.0);

    -- resize lines
    gui.drawLine(context.cairo.layer,
        {er.x + 1, er.y + 4, er.width - 2, 0},
        0.2, 0.2, 0.2, 0.7,
        width: 1);

    gui.drawLine(context.cairo.layer,
        {er.x + 1, er.y + er.height - 4, er.width - 2, 0},
        0.2, 0.2, 0.2, 0.7,
        width: 1);
}

fn drawHorizontal(rect: gui.Rect, data: *Data) {
    data.horizontal = true;
    data.rect = rect;

    er := effectiveRect(data);

    gui.drawRect(context.cairo.layer,
        er,
        cast(f64) 107.0/255.0, cast(f64) 117.0/255.0, cast(f64) 123.0/255.0);

    -- resize lines
    gui.drawLine(context.cairo.layer,
        {er.x + 4, er.y + 1, 0, er.height - 2},
        0.2, 0.2, 0.2, 0.7,
        width: 1);

    gui.drawLine(context.cairo.layer,
        {er.x + er.width - 4, er.y + 1, 0, er.height - 2},
        0.2, 0.2, 0.2, 0.7,
        width: 1);
}

fn mouseButtonDown(messageData: *sdl.MouseButtonEvent, data: *Data) {
    if data.mouseOverBeginResize == true {
        data.resizingBegin = true;
    }
    else if data.mouseOverEndResize == true {
        data.resizingEnd = true;
    }
    else if effectiveRect(data)|gui.containsMouse {
        data.dragging = true;
        data.dragStartBegin = data.begin;
        data.dragStartEnd = data.end;

        if data.horizontal == true {
            data.dragStartMouse = context.mouse.x;
        }
        else {
            data.dragStartMouse = context.mouse.y;
        }
    }
}

fn mouseMove(data: *Data) {
    data.mouseOverBeginResize = false;
    data.mouseOverEndResize = false;

    localX := context.mouse.x - data.rect.x;
    localY := context.mouse.y - data.rect.y;

    if data.dragging == true {
        if data.horizontal == true {
            differential := context.mouse.x - data.dragStartMouse;
            if data.dragStartBegin + differential < 0 {
                differential = -data.dragStartBegin;
            }
            if data.dragStartEnd + differential > data.rect.width {
                differential = data.rect.width - data.dragStartEnd;
            }

            data.end = data.dragStartEnd + differential;
            data.begin = data.dragStartBegin + differential;
        }
        else {
            differential := context.mouse.y - data.dragStartMouse;
            if data.dragStartBegin + differential < 0 {
                differential = -data.dragStartBegin;
            }
            if data.dragStartEnd + differential > data.rect.height {
                differential = data.rect.height - data.dragStartEnd;
            }

            data.begin = data.dragStartBegin + differential;
            data.end = data.dragStartEnd + differential;
        }
    }
    else if data.resizingBegin == true {
        if data.horizontal == true {
            data.begin = localX;
        }
        else {
            data.begin = localY;
        }

        if data.begin < 0 {
            data.begin = 0;
        }
    }
    else if data.resizingEnd == true {
        if data.horizontal == true {
            data.end = math.min(localX, data.rect.width);
        }
        else {
            data.end = math.min(localY, data.rect.height);
        }
    }
    else {
        beginResize: gui.Rect;
        endResize: gui.Rect;

        if data.horizontal == true {
            beginResize = {data.rect.x + data.begin, data.rect.y, 4, data.rect.height};
            endResize = {data.rect.x + data.end - 4, data.rect.y, 4, data.rect.height};
        }
        else {
            beginResize = {data.rect.x, data.rect.y + data.begin, data.rect.width, 4};
            endResize = {data.rect.x, data.rect.y + data.end - 4, data.rect.width, 4};
        }

        resizeCursor: *sdl.Cursor;
        if data.horizontal == true {
            resizeCursor = context.arrowCursorWE;
        }
        else {
            resizeCursor = context.arrowCursorNS;
        }

        if beginResize|gui.containsMouse {
            data.mouseOverBeginResize = true;
            sdl.SDL_SetCursor(resizeCursor);
        }
        else if endResize|gui.containsMouse {
            data.mouseOverEndResize = true;
            sdl.SDL_SetCursor(resizeCursor);
        }
    }
}

fn mouseButtonUp(data: *Data) {
    data.dragging = false;

    data.mouseOverBeginResize = false;
    data.mouseOverEndResize = false;

    data.resizingBegin = false;
    data.resizingEnd = false;
}
