#import "gui/gui";
#import "io";
#import "buffer": buf;
#import "sdl";
#import "math";
#import "iter";

type Note struct {
    rect: gui.Rect!(f64),
    selected: bool
}

type GraphRollFormData struct {
    rect: gui.Rect!(f64),
    bounds: gui.Rect!(f64),

    notes: buf.Buffer!(Note),

    selecting: bool,
    selectStartX: f64,
    selectStartY: f64,

    draggingNotes: bool,
    dragStartX: f64,
    dragStartY: f64,

    mouseInResizeArea: bool,
    resizingNotes: bool,
    resizeStartX: f64,

    xResolution: f64 = 10,
}

fn selectArea(state: GraphRollFormData) gui.Rect!(f64) {
    startX := state.selectStartX;
    startY := state.selectStartY;
    endX := context.mouse.x;
    endY := context.mouse.y;

    if startX > endX {
        startX = context.mouse.x;
        endX = state.selectStartX;
    }

    if startY > endY {
        startY = context.mouse.y;
        endY = state.selectStartY;
    }

    return {
        startX,
        startY,
        endX - startX,
        endY - startY
    };
}

fn yResolution(state: GraphRollFormData) {
    return state.rect.height / 20;
}

fn effectiveRect(note: Note, state: GraphRollFormData) gui.Rect!(f64) {
    offsetX: f64 = 0;
    offsetY: f64 = 0;

    localX := context.mouse.x - state.rect.x;
    localY := context.mouse.y - state.rect.y;

    resizeX: f64 = 0;

    yr := yResolution(state);
    noteY := math.floor(localY / yr);

    if note.selected == true and state.draggingNotes == true {
        offsetX = context.mouse.x - state.dragStartX;
        offsetY = noteY - state.dragStartY;
    }

    if note.selected == true and state.resizingNotes == true {
        resizeX = context.mouse.x - state.resizeStartX;
    }

    hs := &context.state.playArea.horizontalScroller;
    scaleX := hs.rect.width / (hs.end - hs.begin);

    scrollOffsetX := hs.begin;

    er: gui.Rect!(f64) = {
        (note.rect.x - scrollOffsetX) * scaleX + offsetX + state.rect.x,
        (note.rect.y + offsetY) * yr + state.rect.y,
        (note.rect.width) * state.xResolution * scaleX + resizeX,
        note.rect.height * yr
    };

    boundingRect := state.bounds;
    boundingRect.x = state.rect.x;

    return gui.clip(er, boundingRect);
}

fn mouseButtonDown(messageData: *sdl.MouseButtonEvent, index: i64) {
    state := &context.state.playArea.graphRolls.buf[index].gr;

    localX := context.mouse.x - state.rect.x;
    localY := context.mouse.y - state.rect.y;

    if messageData.button == sdl.MouseButton.LEFT {
        didClickNote := false;
        noteClicked: *Note = nil;

        -- possibly select existing notes
        i := 0;
        for note: state.notes.buf|iter.byRef {
            if effectiveRect(^note, ^state)|gui.contains(context.mouse.x, context.mouse.y) {
                didClickNote = true;
                noteClicked = note;

                i += 1;
            }
        }

        if didClickNote == true {
            if context.modifierKeys.ctrl == false and noteClicked.selected == false {
                -- clear all selected notes
                for note: state.notes.buf|iter.byRef {
                    note.selected = false;
                }
            }

            noteClicked.selected = !context.modifierKeys.shift;
            if noteClicked.selected == true {
                if state.mouseInResizeArea == true {
                    state.resizingNotes = true;
                    state.resizeStartX = context.mouse.x;
                }
                else {
                    state.draggingNotes = true;
                    state.dragStartX = context.mouse.x;

                    yr := yResolution(^state);
                    noteY := math.floor(localY / yr);
                    state.dragStartY = noteY;
                }
            }

            return;
        }
        else if context.modifierKeys.ctrl == true {
            -- box select
            state.selecting = true;
            state.selectStartX = context.mouse.x;
            state.selectStartY = context.mouse.y;

            return;
        }

        if context.modifierKeys.shift {
            for note: state.notes.buf|iter.byRef {
                note.selected = false;
            }
            return;
        }

        for note: state.notes.buf|iter.byRef {
            note.selected = false;
        }

        -- add a new one
        hs := &context.state.playArea.horizontalScroller;
        scaleX := hs.rect.width / (hs.end - hs.begin);

        yr := yResolution(^state);
        noteY := math.floor(localY / yr);

        noteRect: gui.Rect!(f64) = {localX / scaleX + hs.begin, noteY, 3, 1};
        &context.state.playArea.graphRolls.buf[index].gr.notes|:append({noteRect, false});
    }
    else if messageData.button == sdl.MouseButton.RIGHT {
        toDelete := false;
        i := 0;
        for note: state.notes.buf|iter.byRef {
            if effectiveRect(^note, ^state)|gui.contains(context.mouse.x, context.mouse.y) {
                if context.modifierKeys.shift == false {
                    note.selected = true;
                    toDelete = true;
                }
            }

            i += 1;
        }

        if toDelete == true {
            writeIndex := 0;
            readIndex := 0;
            while readIndex < state.notes.buf.count {
                if state.notes.buf[readIndex].selected == false {
                    state.notes.buf[writeIndex] = state.notes.buf[readIndex];
                    writeIndex += 1;
                }

                readIndex += 1;
            }

            state.notes.buf.count = writeIndex;
        }
    }
}

fn mouseButtonUp(messageData: *sdl.MouseButtonEvent, index: i64) {
    state := &context.state.playArea.graphRolls.buf[index].gr;

    hs := &context.state.playArea.horizontalScroller;
    scaleX := (hs.end - hs.begin) / hs.rect.width;

    localX := context.mouse.x - state.rect.x;
    localY := context.mouse.y - state.rect.y;

    if state.draggingNotes == true {
        yr := yResolution(^state);
        dragStopY := math.floor(localY / yr);

        for note: state.notes.buf|iter.byRef {
            if note.selected == true {
                note.rect.x += (context.mouse.x - state.dragStartX) * scaleX;
                note.rect.y += dragStopY - state.dragStartY;
            }
        }

        state.draggingNotes = false;
    }

    if state.resizingNotes == true {
        for note: state.notes.buf|iter.byRef {
            if note.selected == true {
                note.rect.width += (context.mouse.x - state.resizeStartX) * scaleX / state.xResolution;
            }
        }

        state.resizingNotes = false;
    }

    state.selecting = false;
}

fn mouseMove(messageData: *sdl.MouseMotionEvent, index: i64) {
    state := &context.state.playArea.graphRolls.buf[index].gr;

    if state.resizingNotes == true {
        return;
    }

    localX := context.mouse.x - state.rect.x;
    localY := context.mouse.y - state.rect.y;

    if state.selecting == false {
        for note: state.notes.buf {
            er := effectiveRect(note, ^state);

            endRect: gui.Rect!(f64) = {
                er.x + er.width - 4,
                er.y,
                4,
                er.height
            };

            if endRect|gui.contains(context.mouse.x, context.mouse.y) {
                sdl.SDL_SetCursor(context.arrowCursorWE);
                state.mouseInResizeArea = true;
                return;
            }
        }

        sdl.SDL_SetCursor(context.arrowCursor);
        state.mouseInResizeArea = false;

        return;
    }

    if context.modifierKeys.ctrl == false {
        state.selecting = false;
        return;
    }

    if state.selecting == true {
        -- convert selectArea to local coordinates
        sa := selectArea(^state);

        for note: state.notes.buf|iter.byRef {
            if effectiveRect(^note, ^state)|gui.intersects(sa) {
                note.selected = !context.modifierKeys.shift;
            }
        }
    }
}

fn quantize(origY: f64, size: f64) {
    return math.floor(origY / size) * size;
}

fn draw(rect: gui.Rect!(f64), bounds: gui.Rect!(f64), index: i64) {
    state := &context.state.playArea.graphRolls.buf[index].gr;
    state.rect = rect;
    state.bounds = bounds;

    -- background
    gui.drawRect(context.cairo.layer, gui.clip(rect, bounds), 0.6, 0.6, 0.9);

    -- outline
    gui.drawOutline(context.cairo.layer, gui.clip(rect, bounds), 0.2, 0.2, 0.2, lineWidth: 1.5);

    yr := yResolution(^state);

    y := rect.y + yr;
    while y < rect.y + rect.height {
        guideLineRect: gui.Rect!(f64) = {rect.x, y, rect.width, 0};
        gui.drawLine(context.cairo.layer, gui.clip(guideLineRect, bounds), 0.2, 0.2, 0.2, lineWidth: 0.5);
        y += yr;
    }

    -- notes
    for note: state.notes.buf {
        er := effectiveRect(note, ^state);

        if note.selected {
            gui.drawRect(context.cairo.layer, er, 1, 1, 0.4);
        }
        else {
            gui.drawRect(context.cairo.layer, er, 0.8, 0.8, 0.2);
        }
    }

    -- select area
    if state.selecting {
        gui.drawRect(context.cairo.layer, selectArea(^state), 0.9, 0.9, 0.9, 0.2);
    }
}
