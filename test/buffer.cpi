#import "mem";

-- todo(chad): capacity should be u64
type Buffer(T := typeof(buf[0])) struct {
    capacity: i64,
    buf: []T
}

type BufferPointer(T: Ast) *Buffer!(T)

fn make(T: Ast)(capacity := 1) {
    buf: []T;
    buf.count = 0;
    buf.data = mem.alloc!(T)(capacity);

    b: Buffer!(T) = {
        capacity: capacity,
        buf: buf
    };

    return mem.heap(b);
}

fn from(T := typeof(t[0]))(t: []T) {
    b := make!(T)(t.count);
    b.buf.count = t.count;
    mem.memcpy(b.buf.data, t.data, t.count * sizeof(T));
    return b;
}

#import "io";

#attr(Buffer) {
fn release(T := typeof(^b))(b: *T/Buffer) {
    mem.free(b.buf.data);
    mem.free(b);
}

fn ensureCapacity(b: !T/Buffer, index: i64) {
    if b.capacity == 0 {
        b.capacity = 1;
    }

    type E typeof(b.buf[0])

    while index >= b.capacity {
        b.capacity = b.capacity * 2;

        newBuf: []E;
        newBuf.count = b.buf.count;
        newBuf.data = mem.alloc!(E)(b.capacity);
        mem.memcpy(newBuf.data, b.buf.data, b.buf.count * sizeof(E));
        mem.free(b.buf.data);

        b.buf = newBuf;
    }
}

fn set( T := typeof(^b), E := typeof(b.buf[0]) )(b: *T/Buffer, index: i64, e: E) {
    ensureCapacity(b, index + 1);

    b.buf[index] = e;
    if b.buf.count <= index {
        b.buf.count = index + 1;
    }
    return b.capacity;
}

fn append( T := typeof(^b), E := typeof(b.buf[0]) )(b: *T/Buffer, e: E) {
    set(b, b.buf.count, e);
}

fn appends( T := typeof(^b), E := typeof(b.buf[0]) )(b: *T/Buffer, e: []E) {
    for _e: e {
        append(b, _e);
    }
}

fn get(b: !T/Buffer, index: i64) {
    ensureCapacity(b, index);
    return b.buf[index];
}
}

fn deleteInPlace(T := typeof(^b))(b: *T, index: i64) {
    readingIndex := 0;
    writingIndex := 0;

    while writingIndex < b.buf.count - 1 {
        if index == readingIndex {
            readingIndex += 1;
        }

        b.buf[writingIndex] = b.buf[readingIndex];

        readingIndex += 1;
        writingIndex += 1;
    }

    b.buf.count -= 1;
}

fn deleteByReordering(T := typeof(^b))(b: *T, index: i64) {
    if b == nil or b.buf.count == 0 {
        return;
    }

    if b.buf.count == 1 {
        b.buf.count = 0;
        return;
    }
    else {
        b.buf[index] = b.buf[b.buf.count - 1];
        b.buf.count -= 1;
    }
}
